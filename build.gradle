/**
 * 构建的配置
 */
buildscript {
    ext {
        springBootVersion = '2.0.6.RELEASE'
        kotlin_version = '1.2.71'
    }
    repositories {
        mavenLocal()
        jcenter()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "io.spring.gradle:dependency-management-plugin:1.0.6.RELEASE"
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

/**
 * All projects configuration
 */
configure(allprojects) { project ->
    group = "io.vincent.learning.stack"
    version = qualifyVersionIfNecessary(version)

    apply plugin: 'idea'
    
    ext {
        springBootVersion = '2.0.6.RELEASE'
        springVersion = '5.0.9.RELEASE'
        springCloudVersion = 'Finchley.SR2'//'Edgware.SR5'
        springBootAdminVersion = '2.0.2'
        platformBomVersion = 'Cairo-SR5'
        unitilsBom = '3.4.2'
        assertjVersion = '3.6.2'
        mybatisSpringBootVersion = '1.3.2'
        lombokVersion = '1.16.22'
    }

    repositories {
        mavenLocal()
        jcenter()
        maven { url "https://repo.spring.io/release" }
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
    }

    idea {
        module {
            //if you hate browsing Javadoc
            downloadJavadoc = false
            //and love reading sources :)
            downloadSources = true
        }
    }
}

/**
 * asion-projects's sub projects configuration
 */
subprojects {
    apply plugin: "io.spring.dependency-management"

    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'kotlin'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    dependencyManagement {
        imports {
            mavenBom "org.springframework:spring-framework-bom:${springVersion}"
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
            mavenBom "io.spring.platform:platform-bom:${platformBomVersion}"
        }
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        compile "org.jetbrains.kotlin:kotlin-reflect"
        testCompile "org.jetbrains.kotlin:kotlin-test"
        testCompile "org.jetbrains.kotlin:kotlin-test-junit"
        testCompile("junit:junit:4.12")
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
        // don't include or exclude anything explicitly by default. See SPR-12085.
    }

    task javadocJar(type: Jar) {
        classifier = "javadoc"
        from javadoc
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    sourceSets {
        main.kotlin.srcDirs += 'src/main/java'
    }

    // noinspection GroovyAssignabilityCheck
    jar {
        manifest {
            attributes 'Created-By': "${System.getProperty("java.version")} (${System.getProperty("java.specification.vendor")})",
                    'Implementation-Title': project.name,
                    'Implementation-Version': project.version
        }
        from("${rootProject.projectDir}/src/dist") {
            include "license.txt"
            include "notice.txt"
            into "META-INF"
            expand(copyright: new Date().format("yyyy"), version: project.version)
        }
    }
}

/**
 * rootProject configuration
 */
configure(rootProject) {
    description = "Vincent's learning stack."
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.8'
}

/*
 * Support publication of artifacts versioned by topic branch.
 * CI builds supply `-P BRANCH_NAME=<TOPIC>` to gradle at build time.
 * If <TOPIC> starts with 'SPR-', change version
 *     from BUILD-SNAPSHOT => <TOPIC>-SNAPSHOT
 *     e.g. 3.2.1.BUILD-SNAPSHOT => 3.2.1.SPR-1234-SNAPSHOT
 */

def qualifyVersionIfNecessary(version) {
    if (rootProject.hasProperty("BRANCH_NAME")) {
        def qualifier = rootProject.property("BRANCH_NAME")
        if (qualifier.startsWith("SPR-")) {
            return version.replace('BUILD', qualifier)
        }
    }
    return version
}
